Attribute VB_Name = "FiguresAndTables"
' Functions for image manipulation in Word
'
' Copyright (c) 2020 Charles Weir
'
'Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
'
'The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
'
'THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
'
Option Explicit


Sub ButtonPressed(control As IRibbonControl)
    'Callback when a button is pressed on the Ribbon
    
    Select Case control.ID
    Case "Figure", "Table"
        InsertFrame (control.ID)
        
    Case "Reposition"
        If SelectedFloatingShape Is Nothing Then
            MsgBox "Please select a floating image, shape or textbox first."
            Exit Sub
        End If
        RepositionFloatingImage
        
    Case "CopyFormat", "PasteFormat"
        If Selection.InlineShapes.Count = 0 Then
            MsgBox "Please select an inline shape first", vbOKOnly
            Exit Sub
        End If
        PreserveImageCroppingAndSizing (control.ID = "PasteFormat")
        
    Case "RelayoutDocument"
        Dim framesToLayout As Collection
        Set framesToLayout = LayoutFloatingImages.ImagesToLayoutInDocument
        If MsgBox("Found " & framesToLayout.Count & " Figures and Tables to layout" & vbCrLf & vbCrLf & _
                "Do you wish to continue?", vbYesNo) <> vbYes Then
            Exit Sub
        End If
        LayoutFloatingImages.LayoutTheseFloatingImages framesToLayout
        
    Case Else
        Debug.Assert False ' Unexpected button ID
    End Select

End Sub


Sub InsertFigure()
' Inserts a floating figure and reference to it.
    InsertFrame "Figure"
End Sub
Sub InsertTable()
' Inserts a floating table and reference to it.
    InsertFrame "Table"
End Sub
Sub CopyImageFormat()
    ' For button or keyboard shortcut (suggest Ctrl-Sh-Cmd C).
    ' Copy the layout of the current image or shape
    
    PreserveImageCroppingAndSizing (False)
End Sub
Sub PasteImageFormat()
    ' For button or keyboard shortcut (suggest Ctrl-Sh-Cmd V).
    ' Paste the layout of the current image or shape
    
    PreserveImageCroppingAndSizing (True)
End Sub

Private Sub InsertFrame(frameType As String)
' Inserts a frame (Figure or Table) at the current selection point.
    Dim newAnchoredFrame As AnchoredFrame
    Set newAnchoredFrame = New AnchoredFrame
    Application.ScreenUpdating = False
    newAnchoredFrame.InitWithNewFrameAt Selection.Range, frameType
    newAnchoredFrame.Update
    Application.ScreenUpdating = True
    Application.ScreenRefresh
End Sub

Private Static Sub PreserveImageCroppingAndSizing(IsPaste)
' Support for updating an image to a new version while preserving cropping and sizing.
' Typically used, for example, with PDFs or jpegs generated by an external tool
'
' Called with IsPaste=False: Takes the selected inline image and copies its size and cropping
' Called with IsPaste=True: Applies the saved size and cropping to the selected image
'
    ' Call is static to preserve value of all variables:
    Dim cropLeft, cropRight, cropTop, cropBottom, pictureHeight, pictureWidth, ScaleHeight, ScaleWidth, Height, Width As Variant
    
    With Selection.InlineShapes(1)
        If Not IsPaste Then ' copy
            ScaleHeight = .ScaleHeight
            ScaleWidth = .ScaleWidth
            Height = .Height
            Width = .Width
            With .PictureFormat
                cropLeft = .cropLeft
                cropRight = .cropRight
                cropTop = .cropTop
                cropBottom = .cropBottom
                pictureHeight = .Crop.pictureHeight
                pictureWidth = .Crop.pictureWidth
            End With
        Else ' Paste
            With .PictureFormat
                .cropLeft = cropLeft
                .cropRight = cropRight
                .cropTop = cropTop
                .cropBottom = cropBottom
            End With
            .ScaleHeight = ScaleHeight
            .ScaleWidth = ScaleWidth
            .Height = Height
            .Width = Width
        End If
    End With
End Sub


Sub RepositionFloatingImage()
'
' Note, doesn't work well as keyboard shortcut, since with keystroke multiple invocations don't seem to work.
'
' Implements support for Latex-like floating pictures near their anchor point.
'
' Specifically, either resets the positioning of the selected floating image or text frame:
'   In two column mode:
'       to be top or bottom of its column or page
'       (first to top, then when called again, to bottom etc)
'   In single column mode:
'      If large, float top or bottom of the page
'      If small, float Left or right, near the anchor
'

    Dim AnchorParagraph As Paragraph

    With SelectedFloatingShape
        If Selection.Sections(1).pageSetup.TextColumns.Count > 1 Then
            ' Column layout. In column if small enough, else page. Toggle top/bottom
            Dim MaxSingleColumnImageWidth As Single
            MaxSingleColumnImageWidth = Selection.Sections(1).pageSetup.TextColumns.Width ' + Selection.Sections(1).pageSetup.TextColumns.Spacing
            .WrapFormat.Type = wdWrapTopBottom
            If .Width > MaxSingleColumnImageWidth Then
                .RelativeHorizontalPosition = wdRelativeHorizontalPositionMargin
            Else
                .RelativeHorizontalPosition = wdRelativeHorizontalPositionColumn
            End If
            .RelativeVerticalPosition = wdRelativeVerticalPositionMargin
            .Left = wdShapeCenter
            If .Top = wdShapeTop Then
                .Top = wdShapeBottom
            Else
                .Top = wdShapeTop
            End If
        Else
            ' One column.
            Dim HalfPageWidth As Single
            HalfPageWidth = Selection.Sections(1).pageSetup.TextColumns.Width / 2
            If .Width < HalfPageWidth Then
                'Small picture. Put near anchor, wrap around. Toggle left/right
                .WrapFormat.Type = wdWrapSquare
                .RelativeVerticalPosition = wdRelativeVerticalPositionLine
                .Top = wdShapeTop  'Or wdShapeCenter, but that looked a bit odd.
                
                .RelativeHorizontalPosition = wdRelativeHorizontalPositionColumn
                If .Left = wdShapeRight Then
                    .Left = wdShapeLeft
                Else
                    .Left = wdShapeRight
                End If
            Else
                ' Big picture: Toggle top/bottom of page
                .WrapFormat.Type = wdWrapTopBottom
                .RelativeVerticalPosition = wdRelativeVerticalPositionMargin
                .RelativeHorizontalPosition = wdRelativeHorizontalPositionColumn
                .Left = wdShapeCenter
                If .Top = wdShapeTop Then
                    .Top = wdShapeBottom
                Else
                    .Top = wdShapeTop
                End If
            End If
        End If
            
    End With

End Sub

Function SelectedFloatingShape() As Shape
    ' Answers the floating shape intended by the user
    ' either a selected floating shape or frame, or a text frame containing the cursor
    If Selection.ShapeRange.Count > 0 Then
        Set SelectedFloatingShape = Selection.ShapeRange(1)
        Exit Function
    End If
    
    ' Find the text frame containing the selection. No easy way I've found...
    Dim currentFrame As Shape
    For Each currentFrame In ActiveDocument.StoryRanges(wdMainTextStory).ShapeRange
        If currentFrame.Type = msoTextBox Then
            If Selection.inRange(currentFrame.TextFrame.TextRange) Then
                Set SelectedFloatingShape = currentFrame
                Exit Function
            End If
        End If
    Next currentFrame
    
    Set SelectedFloatingShape = Nothing
End Function
